plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'maven'
    id 'signing'
}

version = "8.0.0"
group = "org.qenherkhopeshef"
archivesBaseName = "jvectclipboard"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    implementation 'com.lowagie:itext:2.1.5'  
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    useGpgCmd()
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'JVectClipboard'
        packaging 'jar'
        description 'Copy/paste library for vector graphics in Swing'
        url 'https://comp.qenherkhopeshef.org/blog/jvectcutandpaste'

        scm {
          connection 'scm:git:git@github.com:rosmord/jvectclipboard.git'
          developerConnection 'scm:git:git@github.com:rosmord/jvectclipboard.git'
          url 'https://github.com/rosmord/jvectclipboard'
        }

        licenses {
          license {
            name 'GNU LESSER GENERAL PUBLIC LICENSE Version 2.1'
            url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.html'
          }
        }

        developers {
          developer {
            id 'rosmord'
            name 'Serge Rosmorduc'
            email 'serge.rosmorduc@qenherkhopeshef.org'
          }
        }
      }
    }
  }
}

// Remove javadoc warning (way too verbose !)
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {        
          options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
}
